# Aplikace HledámBoha
# (c) 2022 Martin Šmídek <martin@smidek.eu>

# ======================================================================================> JEDNOTLIVE
# rozesílání jednotlivých dopisů
panel mai {type:'right', title:"[fa-send-o] Potvrzení mailem", _sys:'*', include:'onload' }
panel pr  {type:'right', title:"[fa-send-o] Dopis mailem", _sys:'*', include:'onload' }
# ==========================================================================================> PROCES
panel Proces [0,0,300,50] { type:'popup' title:'průběh výpočtu' 
  var parent:ezer,    // vstup - parent je panel, ve kterém je obsažena procedura se 
      refresh:text,   //         jménem v refresh volaná s id_mail po každé dávce
      sent:number,    // výstup - počet odeslaných mailů
      problem:text    //          text případné chyby
  use it: form _it 
  // Start - odstartuje výpočet definovaný objektem par a zobrazí jeho průběh 
  func Start(par,title) { 
    clear(); continue= 1; it.msg= ''; Show(0,1); it.display(1,'t'); it.display(0,'e'); 
    panel.title= title; sent= 0; problem= ''; 
    // inicializace cyklu a předání prametrů - počáteční hodnota todo bude určena z par
    y= object('todo',0,'done',0,'par',par); 
    // zahájení cyklu 
    fork.panel.Step(); // asynchronní spuštění, aby bylo provedeno panel.modal
    panel.modal() 
  }
  // Step - jeden krok procesu
  func Step() { 
    while (continue) {  // po Stop (třeba stisku Cancel) se výpočet přeruší
      y= php.mail2_mai_sending(y); // funkce serveru odpovídá  za done<=todo 
      sent= sent + y.sent;
      it.msg= `posláno ${sent}, zbývá ${y.msg} ${continue?'':'-- PŘERUŠENO!'}`;
      if (y.error) {
        Stop(); problem= y.error; it.msg= `PŘERUŠENO -- CHYBA: ${problem}`
      };
      if (y.max) {
        Stop(); problem= y.max; it.msg= `DOSAŽENO MAXIMA DNEŠNÍ dávky: ${problem}  -- PŘERUŠENO!`;
      };
      if (y.last && refresh) {
        parent.call(refresh,y.last)
      };
      Show(y.done,y.todo); // posune termometr
      if (y.done==y.todo)  
        Stop(); 
    }
    End()
  }
  // stavové proměnné
  var y:object,     // střadač zajištující komunikaci mezi klientem a serverem
      continue=1
  // Show(cast,celek) - posune termometr
  func Show(cast,celek) { 
    it.thermo1.property({width:(cast*290)/celek}) }
  // Stop - zastaví a nechá zobrazené
  func Stop() { continue= 0; it.display(0,'b'); }
  // End - zruší zobrazení
  func End() { it.display(0,'b'); it.display(1,'e') }
  form _it { 
    label thermo1 [6,1,0,8]   { tag:'t', title:'' style:'background-color:#ef7f13' }
    label thermo2 [5,0,290,8] { tag:'t', title:'' style:'border:1px dotted grey' }
    label msg [0,15,300,40]   { tag:'t' format:'c' }
    button cancel [120,30,,]  { tag:'t,b', title:'Přerušit' func onclick () { Stop() } }
    button ok     [136,30,,]  { tag:'e', title:'Ok' func onclick () { panel.hide(1) } }
  }
}
# ============================================================================================= MAPY
map cis_zpusob: table _cis {where:"druh='k_zpusob'", order:'poradi', key_id:'data'}
map cis_ucet: table _cis {where:"druh='k_ucet'", order:'poradi', key_id:'data'}
map cis_smtp: table _cis {where:"druh='smtp_srv'", order:'poradi', key_id:'data'}
